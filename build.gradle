plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.3.10'
    id 'application'
    id 'distribution'
    id 'antlr'
    id 'maven-publish'
    id 'com.gradleup.shadow' version '9.3.1'
    id("pl.allegro.tech.build.axion-release") version "1.21.1"
}

kotlin {
    jvmToolchain(25)
}

group 'gh.marad.chi'
version = scmVersion.version

java {
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/chi-lang/compiler"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

application {
    mainClass = 'gh.marad.chi.MainKt'
}

run {
    standardInput = System.in

}

repositories {
    mavenCentral()
}

dependencies {
    antlr "org.antlr:antlr4:4.12.0"
//    implementation 'org.jgrapht:jgrapht-core:1.5.1'
    implementation 'party.iroiro.luajava:luajit:3.5.0'
    runtimeOnly 'party.iroiro.luajava:luajit-platform:3.5.0:natives-desktop'
    implementation 'party.iroiro.luajava:luajava:3.5.0'
//    implementation 'party.iroiro.luajava:lua54:3.5.0'
//    runtimeOnly 'party.iroiro.luajava:lua54-platform:3.5.0:natives-desktop'
    testImplementation 'io.kotest:kotest-runner-junit5:5.9.1'
    testImplementation("org.junit.jupiter:junit-jupiter")
}

test {
    useJUnitPlatform()
}

generateGrammarSource {
    arguments += ["-visitor", "-no-listener", "-package", "gh.marad.chi.core.antlr"]
    outputDirectory = new File("src/main/java/gh/marad/chi/core/antlr")
}

compileKotlin {
    dependsOn(generateGrammarSource)
}

compileTestKotlin {
    dependsOn(generateTestGrammarSource)
}

sourcesJar {
    dependsOn(generateGrammarSource)
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

shadowJar {
    archiveFileName = "chi-all.jar"
}