plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.6.21'
    id 'application'
    id 'distribution'
    id 'antlr'
    id 'maven-publish'
    id("pl.allegro.tech.build.axion-release") version "1.15.4"
}

group 'gh.marad.chi'
version = scmVersion.version

java {
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/chi-lang/compiler"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

application {
    mainClass = 'gh.marad.chi.MainKt'
}

repositories {
    mavenCentral()
}

dependencies {
    antlr "org.antlr:antlr4:4.12.0"
    implementation 'org.jgrapht:jgrapht-core:1.5.1'
    testImplementation 'io.kotest:kotest-runner-junit5:5.0.0.M3'
    testImplementation("org.junit.jupiter:junit-jupiter")
}

test {
    useJUnitPlatform()
}

generateGrammarSource {
    arguments += ["-visitor", "-no-listener", "-package", "gh.marad.chi.core.antlr"]
    outputDirectory = new File("src/main/java/gh/marad/chi/core/antlr")
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_9
    targetCompatibility = JavaVersion.VERSION_1_9
}

compileKotlin {
    dependsOn(generateGrammarSource)
    kotlinOptions {
        jvmTarget = '9'
    }
}

compileTestKotlin {
    dependsOn(generateTestGrammarSource)
}

sourcesJar {
    dependsOn(generateGrammarSource)
}